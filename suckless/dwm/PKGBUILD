# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'st' 'dmenu')
install=dwm.install
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
    dwm-r1615-selfrestart.diff
    dwm-fakefullscreen-20170508-ceac8c9.diff
    dwm-attachaside-20180126-db22360.diff
    dwm-focusadjacenttag-6.0.diff
    dwm-pertag-20170513-ceac8c9.diff
    dwm-scratchpad-20191009-f46af6d5dbcd06cb8b9a2ab154349ddd.diff
    0001-dwm-Use-Mod-Space-for-launcher.diff
    0002-dwm-Add-keepmenu-config.diff
	dwm.desktop)
sha256sums=('97902e2e007aaeaa3c6e3bed1f81785b817b7413947f1db1d3b62b8da4cd110e'
            '1e9b731366201bc5d84bd0cd1fd140a6cc5756f5cd966f4e48a62160a58c4ad3'
            '3ab0872f66ca32ccef66e851a07087456a69fad6e7729dbec08f78e7ee93b0f3'
            '822a185eaaa43245994505cdcc14402fb67adf3280cdaaf7200451d67e9f463d'
            '5dd9239698ba73fea6d87d008ac4c9b08a8af50caaa5828d5bc9dd068eb7474f'
            '11738a343ce3e5092b7eacf08f6f5c638f14fba2b2f8fe82d9b8e1864c949e22'
            'e791342407b0b2a3140d958d8c4dde3910b353713f32f403e795c2fcb81a82c1'
            'f5326f558d3da5b03e71ce7c0bfc6c0f3f79fc7091ca1000cd206edf9214f851'
            'c5179ae9b5d69301db7321d615ddb07a08a198ef1d5d2217cb74dd2f28487fa2'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch --strip=1 --input="${srcdir}/dwm-r1615-selfrestart.diff"
  patch --strip=1 --input="${srcdir}/dwm-fakefullscreen-20170508-ceac8c9.diff"
  patch --strip=1 --input="${srcdir}/dwm-attachaside-20180126-db22360.diff"
  patch --strip=1 --input="${srcdir}/dwm-focusadjacenttag-6.0.diff"
  patch --strip=1 --input="${srcdir}/dwm-pertag-20170513-ceac8c9.diff"
  patch --strip=1 --input="${srcdir}/dwm-scratchpad-20191009-f46af6d5dbcd06cb8b9a2ab154349ddd.diff"
  patch --strip=1 --input="${srcdir}/0001-dwm-Use-Mod-Space-for-launcher.diff"
  patch --strip=1 --input="${srcdir}/0002-dwm-Add-keepmenu-config.diff"
  # cp "$srcdir/config.h" config.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}
